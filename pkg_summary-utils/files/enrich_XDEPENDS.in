#!/usr/bin/env runawk

#env "LC_ALL=C"

#use "psu_funcs.awk"

#
# usage: enrich_XDEPENDS - <filename>
#

FILENAME == "-" {
	if (/^PKGPATH=/){
		pkgpath = substr($0, 9)
		idx = index(pkgpath, ":")
		if (idx){
			assigns = substr(pkgpath, idx+1)
			pkgpath = substr(pkgpath, 1, idx-1)
		}
	}else if (/^PKGNAME=/){
		pkgbase = substr($0, 9)
		sub(/-[^-]+$/, "", pkgbase)
	}else if (/^ASSIGNMENTS=/){
		assigns = substr($0, 13)
	}else if (NF == 0){
		if (assigns == "" || !((pkgpath SUBSEP pkgbase) in p2a))
			p2a [pkgpath, pkgbase] = assigns

		assigns = pkgpath = pkgbase = ""
	}
	next
}

match($0, /^(BUILD_)?DEPENDS=/) {
	field = substr($0, 1, RLENGTH)
	$0 = substr($0, RLENGTH+1)
	for (i=1; i <= NF; ++i){
		idx = index($i, ":")
		pkgpath = normalize_pkgpath(substr($i, idx+1))
		pkgbase = pkgname2pkgbase(substr($i, 1, idx-1))
#		print "??????????????????????????", pkgpath, pkgbase
		if (((pkgpath SUBSEP pkgbase) in p2a) && p2a [pkgpath, pkgbase] != ""){
			$i = $i ":" p2a [pkgpath, pkgbase]
		}
	}
	print field $0
	next
}

{
	print $0
}
